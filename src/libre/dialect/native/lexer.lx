#
# Copyright 2008-2017 Katherine Flavel
#
# See LICENCE for the full copyright terms.
#

'('  -> $opensub;
')'  -> $closesub;

# predicates
'^'  -> $start;
'$'  -> $end;

# operators
'?'  -> $opt;
'*'  -> $star;
'+'  -> $plus;
'.'  -> $any;
'|'  -> $alt;

# standard escapes
'\\' -> $esc;
'\^' -> $esc;
'\$' -> $esc;
'\?' -> $esc;
'\*' -> $esc;
'\+' -> $esc;
'\.' -> $esc;
'\|' -> $esc;
'\(' -> $esc; '\)' -> $esc;
'\{' -> $esc; # note no \}
'\[' -> $esc; # note no \]

# whitespace escapes
'\f' -> $esc;
'\n' -> $esc;
'\r' -> $esc;
'\t' -> $esc;
'\v' -> $esc;

'\'  /[0-7]{1,3}/   -> $oct;
'\x' /[0-9a-f]{2}/i -> $hex;

'[' -> $opengroup, '[^' -> $opengroupinv, '[]' -> $opengroupcb, '[^]' -> $opengroupinvcb .. ']' -> $closegroup, '-]' -> $closegrouprange {
	'-' -> $range;

	# extension escapes
	'\\' -> $esc;
	'\^' -> $esc;
	'\-' -> $esc;

	# whitespace escapes
	'\f' -> $esc;
	'\n' -> $esc;
	'\r' -> $esc;
	'\t' -> $esc;
	'\v' -> $esc;

	'\[' -> $esc;
	'\]' -> $esc;

	'\'  /[0-7]+/       -> $oct;
	'\x' /[0-9a-f]{2}/i -> $hex;

	'[:alnum:]'  -> $named__class;
	'[:alpha:]'  -> $named__class;
	'[:ascii:]'  -> $named__class;
	'[:cntrl:]'  -> $named__class;
	'[:digit:]'  -> $named__class;
	'[:graph:]'  -> $named__class;
	'[:hspace:]' -> $named__class;
	'[:lower:]'  -> $named__class;
	'[:print:]'  -> $named__class;
	'[:punct:]'  -> $named__class;
	'[:space:]'  -> $named__class;
	'[:upper:]'  -> $named__class;
	'[:vspace:]' -> $named__class;
	'[:word:]'   -> $named__class;
	'[:xdigit:]' -> $named__class;

	# XXX: /./ - the above
	/[^\-\\]/ -> $char;
}

'{' -> $opencount .. '}' -> $closecount {
	','      -> $sep;
	/[0-9]+/ -> $count;
}

/./ - ($opensub | $closesub | $start | $end
	| $opt | $star | $plus | $any | $alt
	| $esc | $oct | $hex
	| $opengroup | $opencount) -> $char;

